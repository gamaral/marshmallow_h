add_definitions(-DMARSHMALLOW_INPUT_LIBRARY)

file(GLOB MARSHMALLOW_INPUT_SRCS *.cpp)
set(MARSHMALLOW_INPUT_LIBS marshmallow_core marshmallow_event)

if(MARSHMALLOW_INPUT_LINUX_EVDEV)
	add_definitions(-DMARSHMALLOW_INPUT_LINUX_EVDEV)
	set(MARSHMALLOW_INPUT_SRCS ${MARSHMALLOW_INPUT_SRCS}
	                           linux/evdev.cpp
	                           linux/evdev_map.cpp
	                           linux/evdev_eventdevice.cpp)

	if(MARSHMALLOW_EVDEV_KEYBOARD)
		add_definitions(-DMARSHMALLOW_EVDEV_KEYBOARD)
		set(MARSHMALLOW_INPUT_SRCS ${MARSHMALLOW_INPUT_SRCS}
		                           linux/evdev_keyboarddevice.cpp)
	endif()

	if(MARSHMALLOW_EVDEV_JOYSTICK)
		add_definitions(-DMARSHMALLOW_EVDEV_JOYSTICK)
		set(MARSHMALLOW_INPUT_SRCS ${MARSHMALLOW_INPUT_SRCS}
		                           linux/evdev_joystickdevice.cpp)
	endif()
endif()

if(MARSHMALLOW_INPUT_UNIX_X11 AND MARSHMALLOW_OPENGL_X11)
	add_definitions(-DMARSHMALLOW_INPUT_UNIX_X11)
	set(MARSHMALLOW_INPUT_SRCS ${MARSHMALLOW_INPUT_SRCS} unix/x11.cpp)
elseif(MARSHMALLOW_INPUT_UNIX_X11)
	message(SEND_ERROR "MARSHMALLOW_INPUT_UNIX_X11 depends on MARSHMALLOW_OPENGL_X11")
endif()

if(MARSHMALLOW_OPENGL_QT4 AND MARSHMALLOW_INPUT_MISC_QT4)
	find_package(Qt4 COMPONENTS QtCore QtGui)
	include(${QT_USE_FILE})
	include_directories(${QT_INCLUDE_DIR})

	add_definitions(-DMARSHMALLOW_INPUT_MISC_QT4)
	set(MARSHMALLOW_INPUT_SRCS ${MARSHMALLOW_INPUT_SRCS} misc/qt4.cpp)
elseif(MARSHMALLOW_INPUT_MISC_QT4)
	message(SEND_ERROR "MARSHMALLOW_INPUT_MISC_QT4 depends on MARSHMALLOW_OPENGL_QT4")
endif()

if(MARSHMALLOW_OPENGL_SDL AND MARSHMALLOW_INPUT_MISC_SDL)
	find_package(SDL)
	include_directories(${SDL_INCLUDE_DIR})

	add_definitions(-DMARSHMALLOW_INPUT_MISC_SDL)
	set(MARSHMALLOW_INPUT_SRCS ${MARSHMALLOW_INPUT_SRCS} misc/sdl.cpp)
elseif(MARSHMALLOW_INPUT_MISC_SDL)
	message(SEND_ERROR "MARSHMALLOW_INPUT_MISC_SDL depends on MARSHMALLOW_OPENGL_SDL")
endif()

if(MARSHMALLOW_OPENGL_WGL AND MARSHMALLOW_INPUT_W32_SYSTEM)
	add_definitions(-DMARSHMALLOW_INPUT_W32_SYSTEM)
	set(MARSHMALLOW_INPUT_SRCS ${MARSHMALLOW_INPUT_SRCS} w32/system.cpp)
elseif(MARSHMALLOW_INPUT_W32_SYSTEM)
	message(SEND_ERROR "MARSHMALLOW_INPUT_W32_SYSTEM depends on MARSHMALLOW_OPENGL_WGL")
endif()

if(MARSHMALLOW_INPUT_QNX_SCREEN AND MARSHMALLOW_OPENGL_QNX)
	add_definitions(-DMARSHMALLOW_INPUT_QNX_SCREEN)
	set(MARSHMALLOW_INPUT_SRCS ${MARSHMALLOW_INPUT_SRCS} qnx/screen.cpp)
elseif(MARSHMALLOW_INPUT_QNX_SCREEN)
	message(SEND_ERROR "MARSHMALLOW_INPUT_QNX_SCREEN depends on MARSHMALLOW_OPENGL_QNX")
endif()

add_library(marshmallow_input ${MARSHMALLOW_INPUT_SRCS})

target_link_libraries(marshmallow_input ${MARSHMALLOW_INPUT_LIBS})

